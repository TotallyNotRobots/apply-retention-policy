# The MIT License (MIT)
#
# Copyright Â© 2025 linuxdaemon <linuxdaemon.irc@gmail.com>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

"""Bazel module for the apply-retention-policy tool."""

module(name = "apply-retention-policy")

bazel_dep(name = "aspect_bazel_lib", version = "2.7.2")
bazel_dep(name = "rules_go", version = "0.54.0")
bazel_dep(name = "gazelle", version = "0.43.0")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "platforms", version = "0.0.10")
bazel_dep(name = "rules_pkg", version = "1.1.0")

bazel_dep(name = "buildifier_prebuilt", version = "8.0.3", dev_dependency = True)

GOLANGCI_LINT_VERSION = "2.1.6"

GOLANGCI_LINT_SHA256 = "e55e0eb515936c0fbd178bce504798a9bd2f0b191e5e357768b18fd5415ee541"

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "golangci_lint",
    build_file_content = """
# Export the versioned binary
exports_files(["golangci-lint-{version}-linux-amd64/golangci-lint"])

# Create a version-independent binary target
alias(
    name = "golangci-lint",
    actual = "golangci-lint-{version}-linux-amd64/golangci-lint",
    visibility = ["//visibility:public"],
)
""".format(version = GOLANGCI_LINT_VERSION),
    sha256 = GOLANGCI_LINT_SHA256,
    urls = ["https://github.com/golangci/golangci-lint/releases/download/v{version}/golangci-lint-{version}-linux-amd64.tar.gz".format(version = GOLANGCI_LINT_VERSION)],
)

# Register Go toolchain
go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.23.9")

# Register Go dependencies
go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(go_deps, "com_github_spf13_cobra", "com_github_spf13_viper", "com_github_stretchr_testify", "org_golang_x_sys", "org_uber_go_zap")

# Register distroless images and make them available
oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_base",
    digest = "sha256:e14f4c00d388743628c674d88e44ac51895d7bc074a37e805eaf141cdbfa5ff1",
    image = "gcr.io/distroless/static-debian12",
    platforms = [
        "linux/amd64",
        "linux/arm64",
    ],
)
use_repo(oci, "distroless_base", "distroless_base_linux_amd64", "distroless_base_linux_arm64")

zstd = use_extension("@aspect_bazel_lib//lib:extensions.bzl", "toolchains")
zstd.zstd()
use_repo(zstd, "zstd_toolchains")

register_toolchains("@zstd_toolchains//:all")
